options {
  JAVA_UNICODE_ESCAPE = true;
}

PARSER_BEGIN(MiniJavaParser)
  package myparser;

  import syntaxtree.*;
  public class MiniJavaParser {}
PARSER_END(MiniJavaParser)

SKIP : /* WHITE SPACE */
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

SPECIAL_TOKEN : /* COMMENTS */
{
  < SINGLE_LINE_COMMENT: "//" (~["\n", "\r"])* ("\n" | "\r" | "\r\n") >
| < FORMAL_COMMENT: "/**" (~["*"])* "*" ("*" | (~["*", "/"] (~["*"])* "*"))* "/" >
| < MULTI_LINE_COMMENT: "/*" (~["*"])* "*" ("*" | (~["*", "/"] (~["*"])* "*"))* "/" >
}

TOKEN :
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LSQPAREN: "[" >
| < RSQPAREN: "]" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < SEMICOLON: ";" >
| < COMMA: "," >
| < DOT: "." >
| < ASSIGN: "=" >
| < LT: "<" >
| < PLUS: "+" >
| < MINUS: "-" >
| < TIME: "*" >
| < AND : "&&" >
| < NOT : "!" >
| < BOOLEAN: "boolean" >
| < CLASS: "class" >
| < ELSE: "else" >
| < EXTENDS: "extends" >
| < FALSE: "false" >
| < IF: "if" >
| < WHILE: "while" >
| < INTEGER: "int" >
| < LENGTH: "length" >
| < MAIN: "main" >
| < NEW: "new" >
| < PUBLIC: "public" >
| < RETURN: "return" >
| < STATIC: "static" >
| < STRING: "String" >
| < THIS: "this" >
| < TRUE: "true" >
| < PRINT: "System.out.println" >
| < VOID: "void" >
}

TOKEN : /* LITERALS */
{
  < INTEGER_LITERAL: (["1"-"9"] (["0"-"9"])* | "0") >
}

TOKEN : /* IDENTIFIERS */
{
  < IDENTIFIER: <LETTER> (<LETTER> | <DIGIT>)* >
| < #LETTER:
      [
       "\u0024",
       "\u0041"-"\u005a",
       "\u005f",
       "\u0061"-"\u007a",
       "\u00c0"-"\u00d6",
       "\u00d8"-"\u00f6",
       "\u00f8"-"\u00ff",
       "\u0100"-"\u1fff",
       "\u3040"-"\u318f",
       "\u3300"-"\u337f",
       "\u3400"-"\u3d2d",
       "\u4e00"-"\u9fff",
       "\uf900"-"\ufaff"
      ]
  >
| < #DIGIT:
      [
       "\u0030"-"\u0039",
       "\u0660"-"\u0669",
       "\u06f0"-"\u06f9",
       "\u0966"-"\u096f",
       "\u09e6"-"\u09ef",
       "\u0a66"-"\u0a6f",
       "\u0ae6"-"\u0aef",
       "\u0b66"-"\u0b6f",
       "\u0be7"-"\u0bef",
       "\u0c66"-"\u0c6f",
       "\u0ce6"-"\u0cef",
       "\u0d66"-"\u0d6f",
       "\u0e50"-"\u0e59",
       "\u0ed0"-"\u0ed9",
       "\u1040"-"\u1049"
      ]
  >
}


/************************************
 * The MiniJava Grammar Starts Here *
 ************************************/


Program Goal() :
{
  MainClass m;
  ClassDeclList cl = new ClassDeclList();
  ClassDecl c;
}
{
  m=MainClass()
  (c=ClassDeclaration() {cl.addElement(c);})*
  <EOF>
  {return new Program(m,cl);}
}

MainClass MainClass() :
{
  IdentifierType i1;
  Identifier i2;
  VarDecl v;
  VarDeclList vl = new VarDeclList();
  Statement s;
}
{
  <CLASS> i1=IdentifierType()
      <LBRACE>
      <PUBLIC> <STATIC> <VOID> <MAIN>
      <LPAREN> <STRING> <LSQPAREN> <RSQPAREN> i2=Identifier() <RPAREN> <LBRACE>
      (LOOKAHEAD(2) v=VarDeclaration() {vl.addElement(v);})*
      s=Statement()
      <RBRACE>
      <RBRACE>
  {return new MainClass(i1, i2, vl, s);}
}

ClassDecl ClassDeclaration() :
{ClassDecl c;}
{
  (
  LOOKAHEAD(3)
  c=ClassDeclarationSimple()
| c=ClassDeclarationExtends()
  )
  {return c;}
}

ClassDecl ClassDeclarationSimple() :
{
  IdentifierType i;
  VarDecl v;
  VarDeclList vl = new VarDeclList();
  MethodDecl m;
  MethodDeclList ml = new MethodDeclList();
}
{
  <CLASS> i=IdentifierType()
      <LBRACE>
      (v=VarDeclaration() {vl.addElement(v);})*
      (m=MethodDeclaration() {ml.addElement(m);})*
      <RBRACE>
  {return new ClassDeclSimple(i, vl, ml);}
}

ClassDecl ClassDeclarationExtends() :
{
  IdentifierType i1, i2;
  VarDecl v;
  VarDeclList vl = new VarDeclList();
  MethodDecl m;
  MethodDeclList ml = new MethodDeclList();
}
{
  <CLASS> i1=IdentifierType() <EXTENDS> i2=IdentifierType()
      <LBRACE>
      (v=VarDeclaration() {vl.addElement(v);})*
      (m=MethodDeclaration() {ml.addElement(m);})*
      <RBRACE>
  {return new ClassDeclExtends(i1, i2, vl, ml);}
}

VarDecl VarDeclaration() :
{
  Type t;
  Identifier i;
}
{
  t=Type() i=Identifier() <SEMICOLON>
  {return new VarDecl(t,i);}
}

MethodDecl MethodDeclaration() :
{
  Type t;
  Identifier i;
  FormalList fl = new FormalList();
  VarDecl v;
  VarDeclList vl = new VarDeclList();
  Statement s;
  StatementList sl = new StatementList();
  Exp e;
}
{
  <PUBLIC> t=Type() i=Identifier()
      <LPAREN> (fl=FormalParameterList())? <RPAREN>
      <LBRACE>
      (LOOKAHEAD(2) v=VarDeclaration() {vl.addElement(v);})*
      (s=Statement() {sl.addElement(s);})*
      <RETURN> e=Expression() <SEMICOLON>
      <RBRACE>
  {return new MethodDecl(t, i, fl, vl, sl, e);}
}

FormalList FormalParameterList() :
{
  FormalList fl = new FormalList();
  Formal f;
}
{
  f=FormalParameter() {fl.addElement(f);}
  (f=FormalParameterRest() {fl.addElement(f);})*
  {return fl;}
}

Formal FormalParameter() :
{
  Type t;
  Identifier i;
}
{
  t=Type() i=Identifier()
  {return new Formal(t, i);}
}

Formal FormalParameterRest() :
{Formal f;}
{
  <COMMA> f=FormalParameter()
  {return f;}
}

Type Type() :
{Type t;}
{
  (
  LOOKAHEAD(3)
  t=ArrayType()
| t=BooleanType()
| t=IntegerType()
| t=IdentifierType()
  )
  {return t;}
}

Type ArrayType() :
{}
{
  <INTEGER> <LSQPAREN> <RSQPAREN>
  {return new IntArrayType();}
}

Type BooleanType() :
{}
{
  <BOOLEAN>
  {return new BooleanType();}
}

Type IntegerType() :
{}
{
  <INTEGER>
  {return new IntegerType();}
}

IdentifierType IdentifierType() :
{Token t;}
{
  t=<IDENTIFIER>
  {return new IdentifierType(t.image);}
}

Statement Statement() :
{Statement s;}
{
  (
  s=Block()
| LOOKAHEAD(2)
  s=AssignmentStatement()
| LOOKAHEAD(2)
  s=ArrayAssignmentStatement()
| s=IfStatement()
| s=WhileStatement()
| s=PrintStatement()
  )
  {return s;}
}

Statement Block() :
{
  Statement s;
  StatementList sl = new StatementList();
}
{
  <LBRACE> (s=Statement() {sl.addElement(s);})* <RBRACE>
  {return new Block(sl);}
}

Statement AssignmentStatement() :
{
  Identifier i;
  Exp e;
}
{
  i=Identifier() <ASSIGN> e=Expression() <SEMICOLON>
  {return new Assign(i, e);}
}

Statement ArrayAssignmentStatement() :
{
  Identifier i;
  Exp e1, e2;
}
{
  i=Identifier() <LSQPAREN> e1=Expression() <RSQPAREN>
      <ASSIGN> e2=Expression() <SEMICOLON>
  {return new ArrayAssign(i, e1, e2);}
}

Statement IfStatement() :
{
  Exp e;
  Statement s1, s2;
}
{
  <IF> <LPAREN> e=Expression() <RPAREN> s1=Statement()
  <ELSE> s2=Statement()
  {return new If(e, s1, s2);}
}

Statement WhileStatement() :
{
  Exp e;
  Statement s;
}
{
  <WHILE> <LPAREN> e=Expression() <RPAREN> s=Statement()
  {return new While(e, s);}
}

Statement PrintStatement() :
{Exp e;}
{
  <PRINT> <LPAREN> e=Expression() <RPAREN> <SEMICOLON>
  {return new Print(e);}
}

Exp Expression() :
{Exp e1, e2;}
{
  e1=ExpressionE()
  (<AND> e2=ExpressionE() {e1 = new And(e1, e2);})*
  {return e1;}
}

Exp ExpressionE() :
{Exp e1, e2;}
{
  e1=ExpressionD()
  (<LT> e2=ExpressionD() {e1 = new LessThan(e1, e2);})*
  {return e1;}
}


Exp ExpressionD() :
{Exp e1, e2;}
{
  e1=ExpressionC()
  (
  <PLUS> e2=ExpressionC() {e1 = new Plus(e1, e2);}
| <MINUS> e2=ExpressionC() {e1 = new Minus(e1, e2);}
  )*
  {return e1;}
}

Exp ExpressionC() :
{Exp e1, e2;}
{
  e1=ExpressionB()
  (<TIME> e2=ExpressionB() {e1 = new Times(e1, e2);})*
  {return e1;}
}

Exp ExpressionB() :
{Exp e;}
{
  (
  <NOT> e=ExpressionB() {e = new Not(e);}
| e=ExpressionA()
  )
  {return e;}
}


Exp ExpressionA() :
{
  Exp e1, e2;
  Identifier i;
  ExpList e3 = new ExpList();
}
{
  e1=PrimaryExpression()
  (
  <LSQPAREN> e2=Expression() <RSQPAREN> {e1 = new ArrayLookup(e1, e2);}
| LOOKAHEAD(3)
  <DOT> i=Identifier()
      <LPAREN>
      (
      e2=Expression() {e3.addElement(e2);}
      (<COMMA> e2=Expression() {e3.addElement(e2);})*
      )?
      <RPAREN>
  {e1 = new Call(e1, i, e3);}
| <DOT> <LENGTH> {e1 = new ArrayLength(e1);}
  )*
  {return e1;}
}

Exp PrimaryExpression() :
{
  Exp e;
  Token t;
  IdentifierType i;
}
{
  (
  t=<INTEGER_LITERAL> {e = new IntegerLiteral(Integer.decode(t.image));}
| <TRUE> {e = new True();}
| <FALSE> {e = new False();}
| t=<IDENTIFIER> {e = new IdentifierExp(t.image);}
| <THIS> {e = new This();}
| LOOKAHEAD(3)
  <NEW> <INTEGER> <LSQPAREN> e=Expression() <RSQPAREN> {e = new NewArray(e);}
| <NEW> i=IdentifierType() <LPAREN> <RPAREN> {e = new NewObject(i);}
| <LPAREN> e=Expression() <RPAREN>
  )
  {return e;}
}

Identifier Identifier() :
{Token t;}
{
  t=<IDENTIFIER>
  {return new Identifier(t.image);}
}
